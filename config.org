* Basics

This is an emacs configuration written as Org file(s). Literate configs are a side of literate programming (https://en.wikipedia.org/wiki/Literate_programming) where configuration is written in a file along with its documentation. From this literate configuration file, configs applied are subject to be extracted into a "runnable" file (in this case into init.el), this process is called "tangling" (important Org functions for this are org-babel-tangle and org-babel-tangle-file, see: https://orgmode.org/manual/Extracting-source-code.html for more info on the subject).

** Basic defaults

Remove all the stuff I don't use.

#+BEGIN_SRC emacs-lisp
(customize-set-variable 'scroll-bar-mode nil)
(customize-set-variable 'horizontal-scroll-bar-mode nil)
(customize-set-variable 'blink-cursor-mode nil)
(customize-set-variable 'tool-bar-mode nil)
#+END_SRC

Yes or no: y-or-n

#+BEGIN_SRC emacs-lisp
(defalias 'yes-or-no-p 'y-or-n-p)
#+END_SRC

Some miscelaneous defaults.
#+BEGIN_SRC emacs-lisp
(setq-default inhibit-startup-screen t
              initial-scratch-message ""
	      	  visible-bell nil                               ; No shaking
	          confirm-nonexistent-file-or-buffer nil         ; Remove confirm dialog on new buffers
	          show-paren-delay 0                             ; No delay when showing matching parenthesis
	          confirm-kill-emacs 'y-or-n-p                   ; Confirm exiting Emacs
	          display-time-default-load-average nil          ; Don't display load average
	          indent-tabs-mode nil                           ; No tabs for indentation
	          select-enable-clipboard t                      ; Unite Emacs & system clipboard
	          window-combination-resize t                    ; Create new windows proportionally
	          use-package-always-ensure t)                    ; Install packages if not found on the system
#+END_SRC

Scrolling defaults.
#+BEGIN_SRC emacs-lisp
(setq scroll-margin 1
      scroll-conservatively 0
      scroll-up-aggressively 0.01
      scroll-down-aggressively 0.01)

(setq-default scroll-up-aggressively 0.01
              scroll-down-aggressively 0.01)
#+END_SRC

UTF-8 everywhere, because it is the standard.

#+BEGIN_SRC emacs-lisp
;; disable CJK coding/encoding (Chinese/Japanese/Korean characters)
(setq utf-translate-cjk-mode nil)

(prefer-coding-system 'utf-8)
(setq default-file-name-coding-system 'utf-8)
(set-default-coding-systems 'utf-8)
(set-terminal-coding-system 'utf-8)
(set-keyboard-coding-system 'utf-8)
(set-language-environment 'utf-8)
(set-language-environment 'utf-8)
(setq locale-coding-system 'utf-8)

(if (boundp buffer-file-coding-system)
    (setq buffer-file-coding-system 'utf-8)
  (setq default-buffer-file-coding-system 'utf-8))

;; Treat clipboard input as UTF-8 string first; compound text next, etc.
(setq x-select-request-type '(UTF8_STRING COMPOUND_TEXT TEXT STRING))
#+END_SRC


** Misc keybindings

Use `C-x C-0` to restore font size.

#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "C-+") 'text-scale-increase)        ; Bigger
(global-set-key (kbd "C--") 'text-scale-decrease)        ; Smaller
(global-set-key (kbd "M-n") 'next-multiframe-window)     ; Cycle through frames
(global-set-key (kbd "M-p") 'previous-multiframe-window) ; Cycle through frames
#+END_SRC

** Theme

Use monokai theme for Emacs.

#+BEGIN_SRC emacs-lisp
(use-package monokai-theme
  :init
  (load-theme 'monokai t))
#+END_SRC



** OS X

Use Steve Purcell's [[https://github.com/purcell/exec-path-from-shell][exec-path-from-shell]] when loading Emacs outside the console.

#+BEGIN_SRC emacs-lisp
(use-package exec-path-from-shell
  :if (memq window-system '(mac ns x))
  :config
  (setq exec-path-from-shell-variables '("PATH" "GOPATH")
        exec-path-from-shell-check-startup-files nil)
  (exec-path-from-shell-initialize))
#+END_SRC

The variables available for binding the modifier keys on Mac keyboards are the following:

- mac-function-modifier
- mac-control-modifier
- mac-command-modifier
- mac-option-modifier
- mac-right-command
- mac-right-control-modifier
- mac-right-option-modifier

The values can be 'control, 'alt, 'meta, 'super, 'hyper, nil (setting to nil allows the OS to assign values). Sources: [[https://www.emacswiki.org/emacs/EmacsForMacOS#toc31][emacswiki.org]] 
[[https://www.gnu.org/software/emacs/manual/html_node/emacs/Mac-_002f-GNUstep-Events.html#Mac-_002f-GNUstep-Events][gnu.org]]
#+BEGIN_SRC emacs-lisp
(when (eq system-type 'darwin)
  (setq-default
   exec-path (append exec-path '("/usr/local/bin"))  ; Add Homebrew path
   mac-command-modifier 'meta                        ; Map Meta to Cmd
   mac-option-modifier nil                           ; Don't use Option key 
   mac-right-option-modifier nil                     ; Disable the right Alt key
   ns-pop-up-frames nil                              ; Visit files in same frame
   dired-use-ls-dired nil))                          ; macOS command ls doesn't support --dired option
#+END_SRC


** Backups

As default write backup files on ~/.emacs.d/backup and disables lock files, it gets messy on mounted network file systems like Keybase FS (https://keybase.io/).

#+BEGIN_SRC emacs-lisp
(setq backup-by-copying t
      backup-directory-alist '(("." . "~/.emacs.d/backup"))
      delete-old-versions t
      kept-new-versions 3
      kept-old-versions 2
      version-control t
      create-lockfiles nil
      vc-follow-symlinks t)
#+END_SRC


* Languages / frameworks

** Docker

#+BEGIN_SRC emacs-lisp
(use-package dockerfile-mode
  :mode "Dockerfile\\'" 
  :mode "Dockerfile.test\\'")
#+END_SRC


** HTML

#+BEGIN_SRC emacs-lisp
(use-package sgml-mode
  :mode "\\.html\\'"
  :config (setq-default sgml-basic-offset 2))
#+END_SRC


** Markdown

#+BEGIN_SRC emacs-lisp
(use-package markdown-mode
  :mode "INSTALL\\'"
  :mode "CONTRIBUTORS\\'"
  :mode "LICENSE\\'"
  :mode "README\\'"
  :mode "\\.markdown\\'"
  :mode "\\.md\\'"
  :config
  (setq-default
   markdown-asymmetric-header t
   markdown-split-window-direction 'right))
#+END_SRC


** YAML

#+BEGIN_SRC emacs-lisp
(use-package yaml-mode
  :mode "\\.yml\\'"  
  :mode "\\.yaml\\'")
#+END_SRC


** CSV

#+BEGIN_SRC emacs-lisp
(use-package csv-mode
  :mode "\\.[Cc][Ss][Vv]\\'"
  :init (setq csv-separators '("," ";" "|" " "))
  :config (use-package csv-nav))
#+END_SRC


** Magit

Magit is love for Emacs.

#+BEGIN_SRC emacs-lisp
(use-package magit
  :config
  (setq-default
   magit-refs-show-commit-count 'all
   magit-section-show-child-count t))
#+END_SRC



** Go

#+BEGIN_SRC emacs-lisp
(use-package go-mode
  :mode "\\.go\\'"
  :mode "\\.toml\\'"
  :init
  (add-hook 'before-save-hook #'gofmt-before-save))

(use-package go-eldoc
  :init
  (add-hook 'go-mode-hook 'go-eldoc-setup))

(use-package go-add-tags)
#+END_SRC


** Ruby

Keystrokes to remember:

C-M-p / C-M-n
    Moves to the beginning / end of a block.
C-M-a / C-M-e
    Moves to the beginning / end of a function.

#+BEGIN_SRC emacs-lisp
(use-package enh-ruby-mode
  :mode "\\.rb\\'"
  :mode "\\.rake\\'"
  :mode "\\.ru\\'"
  :mode "Gemfile\\'"
  :mode "Rakefile\\'"
  :mode "Capfile\\'" 
  :mode "\\.gemspec\\'"
  :config
  (setq enh-ruby-add-encoding-comment-on-save nil
        enh-ruby-deep-indent-paren nil
        enh-ruby-hanging-brace-indent-level 2
        enh-ruby-use-encoding-map nil
        rspec-autosave-buffer t
        rspec-compilation-buffer-name "*rspec-compilation*"
        rspec-use-opts-file-when-available nil
        rspec-use-rake-flag nil
        ruby-deep-arglist nil
        ruby-deep-indent-paren nil
        ruby-end-insert-newline nil
        ruby-insert-encoding-magic-comment nil
        ruby-indent-level 2
        ruby-indent-tabs-mode nil)
)
#+END_SRC


*** Ruby documentation

Uses [[https://www.emacswiki.org/emacs/YARI][YARI]] to display Ruby documentation.

#+BEGIN_SRC emacs-lisp
(use-package yari
  :after enh-ruby-mode
  :init
  (add-hook 'enh-ruby-mode-hook (lambda () (local-set-key [f1] 'yari))))
#+END_SRC

Use F1 when placing the point on some function. To generate the documentation

#+BEGIN_SRC
gem rdoc --all --ri --no-rdoc
rvm docs generate all
#+END_SRC


*** Ruby REPL

#+BEGIN_SRC emacs-lisp
(use-package inf-ruby
  :after enh-ruby-mode
  :init
  (add-hook 'enh-ruby-mode-hook 'inf-ruby-minor-mode))

#+END_SRC


*** Ruby tests

Uses [[http://rspec.info/][RSpec]] which is the defacto test suite for Ruby development.

#+BEGIN_SRC emacs-lisp
(use-package rspec-mode
  :after enh-ruby-mode)
#+END_SRC


*** Ruby environment manager

Uses [[https://rvm.io/][RVM]] to manage gems.

#+BEGIN_SRC emacs-lisp
(use-package rvm
  :after enh-ruby-mode
  :config
  (rvm-use-default))
#+END_SRC


** Cucumber

Use [[https://cucumber.io/][Cucumber]] for BDD

#+BEGIN_SRC emacs-lisp
(use-package feature-mode
  :commands feature-mode
  :config
  (setq feature-default-language "en"))
#+END_SRC


* Features


** Which-key

#+BEGIN_SRC emacs-lisp
 (use-package which-key
  :init
  (add-hook 'after-init-hook 'which-key-mode)
  :config
  (which-key-mode)
  (setq which-key-idle-delay 0.25))
#+END_SRC




** Ace

Use ace-window to cycle through windows

#+BEGIN_SRC emacs-lisp
(use-package ace-window
  :config
  :bind* ("M-o" . ace-window))
#+END_SRC



** Ibuffer

#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "C-x C-b") 'ibuffer)
#+END_SRC



** Encryption

EasyPG is used for encryption. More info ([[https://www.emacswiki.org/emacs/EasyPG]])

#+BEGIN_SRC emacs-lisp
(setq epg-gpg-program "gpg2")
(setf epa-pinentry-mode 'loopback)
(setq epa-file-inhibit-auto-save t)
#+END_SRC

#+BEGIN_SRC emacs-lisp
(use-package pinentry
  :config
  (pinentry-start))
#+END_SRC



** Misc

#+BEGIN_SRC emacs-lisp
(dolist (mode
  '(global-prettify-symbols-mode        ; Greek letters should look greek
    global-auto-revert-mode             ; Reload files when change, please
    show-paren-mode                     ; Highlight matching parentheses
    cua-mode                            ; Global Cmd-c, Cmd-x to copy & paste
    global-display-line-numbers-mode    ; Native line numbers
    ))
  (funcall mode 1))
#+END_SRC




** Tramp mode

Use tramp to shell into other machines.

#+BEGIN_SRC emacs-lisp
(use-package tramp
  :config
  (tramp-set-completion-function "ssh" '((tramp-parse-sconfig "/etc/ssh_config") (tramp-parse-sconfig "~/.ssh/config"))))
#+END_SRC




** Smart parens

Use smart parens when writing parenthesis to not let any parethesis unmatched.

#+BEGIN_SRC emacs-lisp
(use-package smartparens
  :ensure t
  :init
    (add-hook 'enh-ruby-mode-hook 'smartparens-strict-mode))
#+END_SRC




** Org

Adding org files for agenda

#+BEGIN_SRC emacs-lisp
(setq org-directory "/keybase/private/spavi/org/")
(require 'find-lisp)
(setq org-agenda-files
      (find-lisp-find-files org-directory "\.org.gpg$"))
(setq org-default-notes-file "/keybase/private/spavi/org/refile.org.gpg")
#+END_SRC

The thick of it

#+BEGIN_SRC emacs-lisp
  (use-package org
    :init
    (setq org-support-shift-select t
          org-return-follows-link t
          org-hide-emphasis-markers t
          org-outline-path-complete-in-steps nil
          org-src-fontify-natively t
          org-src-tab-acts-natively t
          org-confirm-babel-evaluate nil
          org-log-done t
          org-refile-targets '((nil :maxlevel . 9) (org-agenda-files :maxlevel . 9))
          org-refile-use-outline-path t
          org-outline-path-complete-in-steps nil
          org-completion-use-ido t
          ido-everywhere t
          ido-max-directory-size 100000
          ido-default-file-method 'selected-window
          ido-default-buffer-method 'selected-window
          org-indirect-buffer-display 'current-window
          org-fast-tag-selection-include-todo t
          org-use-fast-todo-selection t
          org-startup-indented t)
    (add-to-list 'auto-mode-alist '("\\.txt\\'" . org-mode))
    (add-to-list 'auto-mode-alist '(".*/[0-9]*$" . org-mode))
    (add-hook 'org-mode-hook 'auto-fill-mode)
    (add-hook 'org-journal-mode-hook 'auto-fill-mode)
    :bind (("C-c l" . org-store-link)
           ("C-c n" . org-capture)
           ("C-c a" . org-agenda))
    :config
    (font-lock-add-keywords
     'org-mode `(("^\\*+ \\(TODO\\) "
                  (1 (progn (compose-region (match-beginning 1) (match-end 1) "⚑") nil)))
                 ("^\\*+ \\(PROGRESSING\\) "
                  (1 (progn (compose-region (match-beginning 1) (match-end 1) "⚐") nil)))
                 ("^\\*+ \\(CANCELED\\) "
                  (1 (progn (compose-region (match-beginning 1) (match-end 1) "✘") nil)))
                 ("^\\*+ \\(DONE\\) "
                  (1 (progn (compose-region (match-beginning 1) (match-end 1) "✔") nil)))))

    (setq org-tag-alist '((:startgroup) ("@w0rk" . ?w) ("@home" . ?h) (:endgroup))
          org-todo-keywords '((sequence "TODO(t)" "PROGRESSING(p)" "|" "DONE(d)")
                              (sequence "INACTIVE(i@/!)" "|" "CANCELLED(c@/!)"))
          org-todo-keyword-faces
          '(("TODO" :foreground "red" :weight bold)
            ("PROGRESSING" :foreground "blue" :weight bold)
            ("DONE" :foreground "forest green" :weight bold)
            ("INACTIVE" :foreground "magenta" :weight bold)
            ("CANCELLED" :foreground "brown" :weight bold)))
    (setq org-agenda-custom-commands
          '(("w" "Work" tags-todo "@w0rk")
            ("h" "Home" tags-todo "@home")))

    (define-key org-mode-map [remap org-return] (lambda () (interactive)
                                                  (if (org-in-src-block-p)
                                                      (org-return) (org-return-indent)))))
#+END_SRC

Org journaling

#+BEGIN_SRC emacs-lisp
(use-package org-journal
  :config
  (setq org-journal-date-format "%A, %d.%m.%Y"
        org-journal-file-format "%Y%m%d"
        org-journal-dir "/keybase/private/spavi/org/diary/"))
#+END_SRC

Org template custom configurations

#+BEGIN_SRC emacs-lisp
(defvar org-capture-templates
  '(
    ("t" "To-do task." 
     entry 
     (file+headline org-default-notes-file "Tasks")
     "* TODO %?\n%u\n%a\n" :clock-in t :clock-resume t)
    ("w" "Work task." 
     entry 
     (file+headline org-default-notes-file "Work Task")
     "* TODO %?\n%u\n%a\n" 
     :clock-in t 
     :clock-resume t)
    ("l" "Link: Something interesting?"
     entry
     (file+headline org-default-notes-file "Links")
     (file "~/.emacs.d/org-templates/links.orgcaptmpl"))
    ("i" "Idea came up." 
     entry 
     (file org-default-notes-file)
     "* %? :IDEA: \n%u" :clock-in t :clock-resume t)))
#+END_SRC

Meeting note taking (source: [[https://github.com/howardabrams/dot-files/][Howard Abrams' Github]])

#+BEGIN_SRC emacs-lisp
(defun meeting-notes ()
  "Call this after creating an org-mode heading for where the notes for the meeting
should be. After calling this function, call 'meeting-done' to reset the environment."
  (interactive)
  (outline-mark-subtree)                              ;; Select org-mode section
  (narrow-to-region (region-beginning) (region-end))  ;; Only show that region
  (deactivate-mark)
  (delete-other-windows)                              ;; Get rid of other windows
  (text-scale-set 2)                                  ;; Text is now readable by others
  (fringe-mode 0)
  (message "When finished taking your notes, run meeting-done."))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun meeting-done ()
  "Attempt to 'undo' the effects of taking meeting notes."
  (interactive)
  (widen)                                       ;; Opposite of narrow-to-region
  (text-scale-set 0)                            ;; Reset the font size increase
  (fringe-mode 1)
  (winner-undo))                                ;; Put the windows back in place
#+END_SRC

Bullets!

#+BEGIN_SRC emacs-lisp
(use-package org-bullets
  :after org
  :hook
  (org-mode . (lambda () (org-bullets-mode 1))))
#+END_SRC

Calendar modifications (Finnish calendar, etc)

#+BEGIN_SRC emacs-lisp
(use-package suomalainen-kalenteri
  :after org
  :config
  (setq calendar-date-style 'european
        calendar-latitude 60.1     ; Roughly Helsinki
        calendar-longitude 24.9    ; Roughly Helsinki
        calendar-week-start-day 1  ; Weeks start on Monday
        calendar-today-visible-hook 'calendar-mark-today
        calendar-holidays suomalainen-kalenteri
        org-agenda-include-diary t))
#+END_SRC

I want my <s TAB expansions back!.

#+BEGIN_SRC emacs-lisp
(add-to-list 'org-modules 'org-tempo)
#+END_SRC

Some org-agenda and habits specific configs.

#+BEGIN_SRC emacs-lisp
(add-to-list 'org-modules 'org-habit)
(setq org-agenda-use-tag-inheritance nil
      org-agenda-ignore-drawer-properties '(effort appt category)
      org-agenda-dim-blocked-tasks nil
      org-agenda-tags-column -55
      org-log-into-drawer t
      org-habit-show-habits-only-for-today nil
      org-habit-graph-column 60
      org-habit-show-all-today t
      org-habit-show-following-days 10
      org-habit-preceding-days 10
      org-habit-show-habits t)
#+END_SRC



** Projectile

Use Projectile for jumping around code, etc.

#+BEGIN_SRC emacs-lisp
(use-package projectile
  :defer 1
  :init
  (setq-default
   projectile-cache-file (expand-file-name ".projectile-cache" user-emacs-directory)
   projectile-keymap-prefix (kbd "C-c C-p")
   projectile-known-projects-file (expand-file-name
                                   ".projectile-bookmarks" user-emacs-directory))
  :config
  (projectile-global-mode 1)
  (setq-default
   projectile-indexing-method 'alien
   projectile-globally-ignored-modes '("org-mode" "org-agenda-mode")
   projectile-globally-ignored-file-suffixes '(".gpg")
   projectile-completion-system 'ido
   projectile-enable-caching t
   projectile-mode-line '(:eval (projectile-project-name)))
   (add-hook 'org-agenda-mode-hook (lambda () (projectile-mode -1)))
   (add-hook 'org-mode-hook (lambda () (projectile-mode -1))))
#+END_SRC


** Highlight

#+BEGIN_SRC emacs-lisp
(use-package highlight)
#+END_SRC




** Treemacs

#+BEGIN_SRC emacs-lisp
(use-package treemacs
  :config
  (setq treemacs-follow-after-init t
        treemacs-width 35
        treemacs-indentation 1
        treemacs-recenter-after-file-follow nil
        treemacs-silent-refresh t
        treemacs-silent-filewatch t
        treemacs-change-root-without-asking t
        treemacs-sorting 'alphabetic-desc
        treemacs-show-hidden-files t
        treemacs-never-persist nil
        treemacs-is-never-other-window f
        treemacs-indentation-string (propertize " ǀ " 'face 'font-lock-comment-face)
        treemacs-follow-mode t
        treemacs-filewatch-mode t
        treemacs-fringe-indicator-mode t)
  :bind
  (([f8] . treemacs)
   ("C-c f" . treemacs-select-window)))

(use-package treemacs-projectile
  :after treemacs projectile
  :bind 
  (("C-c o p" . treemacs-projectile)))
#+END_SRC


** Yafolding

#+BEGIN_SRC emacs-lisp
(use-package yafolding
  :bind 
  (("M-RET" . yafolding-toggle-element)
  ("M-m" . yafolding-toggle-all))
  :init
  (add-hook 'enh-ruby-mode-hook 'yafolding-mode))
  ;;(define-key yafolding-mode-map (kbd "M-m") 'yafolding-toggle-all)  
  ;;(define-key yafolding-mode-map (kbd "C-RET") 'yafolding-toggle-element)
#+END_SRC
  
